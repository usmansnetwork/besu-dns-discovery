// SPDX-License-Identifier: Apache-2.0
plugins {
    alias(libs.plugins.jvm)
    alias(libs.plugins.jreleaser)
    id 'java-library'
    id 'maven-publish'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    //api libs....

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation libs.bundles.tuweni
    implementation libs.slf4j.api
    implementation libs.bundles.vertx

    compileOnly libs.bcprov.jdk15on

    testImplementation libs.tuweni.junit
    testImplementation libs.bcprov.jdk15on
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use Kotlin Test test framework
            useKotlinTest('1.9.22')
        }
    }
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'info.usmans.tools'
            artifactId = 'besu-dns-discovery'

            from components.java

            pom {
                name = 'besu-dns-discovery'
                description = 'Fork of Tuweni DNS Resolver'
                url = 'https://github.com/usmansnetwork/besu-dns-discovery'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'usmansaleem'
                        name = 'Usman Saleem'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/usmansnetwork/besu-dns-discovery.git'
                    developerConnection = 'scm:git:ssh://git@github.com:usmansnetwork/besu-dns-discovery.git'
                    url = 'https://github.com/usmansnetwork/besu-dns-discovery.git'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}
